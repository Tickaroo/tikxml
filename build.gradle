buildscript {
    repositories {
        mavenCentral()
        maven {
            url "http://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: property('kotlinVersion')
    }
}

plugins {
    id 'net.ltgt.apt' version '0.10'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: "jacoco"

    group = 'com.tickaroo.tikxml'
    version = '0.8.11-SNAPSHOT'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
        maven {
            url "http://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    jacoco {
        toolVersion = property('jacocoVersion')
    }

    findbugs {
        ignoreFailures = true
        effort = "max"
        reportLevel = "low"
        reportsDir = file("$project.buildDir/findbugs")
        sourceSets = [sourceSets.main]
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: property('junitVersion')
        testCompile group: 'org.assertj', name: 'assertj-core', version: property('assertjVersion')
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}


project(':core') {
    dependencies {
        compile group: 'com.squareup.okio', name: 'okio', version: property('okioVersion')
    }
}

project(':converter-htmlescape') {
    dependencies {
        compile project(':core')
    }
}

project(':converter-date-rfc3389') {
    dependencies {
        compile project(':core')
    }
}

project(':annotation') {
    dependencies {
        compile project(':core')
    }
}

project(':processor-common') {
    apply plugin: 'kotlin'

    findbugs {
        sourceSets = []
    }

    dependencies {
        compile project(':annotation')

        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: property('kotlinVersion')
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre7', version: property('kotlinVersion')
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: property('kotlinVersion')
    }
}

project(':processor') {
    apply plugin: 'kotlin'

    findbugs {
        sourceSets = []
    }

    dependencies {
        compile project(':processor-common')
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: property('kotlinVersion')
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre7', version: property('kotlinVersion')
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: property('kotlinVersion')

        compile group: 'com.squareup', name: 'javapoet', version: property('javaPoetVersion')
        compile group: 'com.google.guava', name: 'guava', version: property('guavaVersion')

        compileOnly group: 'com.google.auto.service', name: 'auto-service', version: property('autoServiceVersion')

        testCompile group: 'com.google.testing.compile', name: 'compile-testing', version: property('compileTestingVersion')
        testCompile group: 'org.mockito', name: 'mockito-core', version: property('mockitoVersion')
        testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: property('kotlinVersion')
    }
}

project(':retrofit-converter') {
    apply plugin: 'net.ltgt.apt'

    dependencies {
        compile project(':annotation')
        compile project(':core')
        compile group: 'com.squareup.retrofit2', name: 'retrofit', version: property('retrofitVersion')

        testApt project(':processor')
        testCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: property('mockWebserverVersion')
    }
}


project(':auto-value-tikxml') {
    apply plugin: 'kotlin'
    apply plugin: 'net.ltgt.apt'

    findbugs {
        sourceSets = []
    }

    dependencies {
        compile project(':processor-common')
        compile group: 'com.google.auto.value', name: 'auto-value', version: property('autoValueVersion')
        compileOnly group: 'com.google.auto.service', name: 'auto-service', version: property('autoServiceVersion')
        compile group: 'com.squareup', name: 'javapoet', version: property('javaPoetVersion')
        compile group: 'com.google.guava', name: 'guava', version: property('guavaVersion')

        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: property('kotlinVersion')
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre7', version: property('kotlinVersion')
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: property('kotlinVersion')

        testCompile group: 'com.google.testing.compile', name: 'compile-testing', version: property('compileTestingVersion')
    }
}

project(':annotation-processor-testing') {
    apply plugin: 'kotlin'
    apply plugin: 'net.ltgt.apt'

    dependencies {
        compile project(':processor-common')
        compile group: 'com.google.auto.value', name: 'auto-value', version: property('autoValueVersion')

        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: property('kotlinVersion')
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre7', version: property('kotlinVersion')
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: property('kotlinVersion')

        testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: property('kotlinVersion')

        apt project(':processor')
        apt project(':auto-value-tikxml')
    }
}

project(':benchmark') {
    apply plugin: 'net.ltgt.apt'

    dependencies {
        compile project(':annotation')
        compile project(':core')
        apt project(':processor')

        compile group: 'org.simpleframework', name: 'simple-xml', version: '2.7.1'
        compile group: 'net.trajano.caliper', name: 'caliper', version: '1.2.1'
        compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.8.8'
    }
}


